#+TITLE: GNU Emacs configuration
#+AUTHOR: Alejandro Polanco
#+EMAIL: apolanco.sosa@gmail.com
#+DATE: 2021-01-28
#+PROPERTY: header-args :tangle ~/.emacs.d/configuration.el

** Table of content:                                                 :TOC_3:
  - [[#initialization][Initialization:]]
    - [[#lexical-binding][Lexical binding]]
    - [[#garbage-collection-optimization][Garbage collection Optimization]]
    - [[#directory-management][Directory management]]
    - [[#custom][Custom]]
    - [[#libraries][Libraries]]
  - [[#package-management][Package management:]]
    - [[#package][Package]]
    - [[#use-package][Use-package]]
  - [[#ui-enhancements][UI Enhancements:]]
    - [[#frame-and-buffer][Frame and buffer]]
    - [[#font][Font]]
    - [[#minibuffer][Minibuffer]]
    - [[#cursor][Cursor]]
    - [[#scrolling][Scrolling]]
    - [[#fringe][Fringe]]
    - [[#general-coding-style][General coding style]]
  - [[#colorscheme][Colorscheme]]
    - [[#modus-themes][Modus-themes]]
  - [[#keybindings][Keybindings:]]
    - [[#global-settings][Global settings]]
    - [[#modal-editing][Modal editing]]
    - [[#mnemonic-interfaces][Mnemonic interfaces]]
  - [[#built-in-packages][Built-in packages:]]
    - [[#saveplace][Saveplace]]
    - [[#paren-mode][Paren-mode]]
    - [[#recentf][recentf]]
    - [[#autorevert][Autorevert]]
    - [[#savehist][savehist]]
    - [[#eldoc][Eldoc]]
    - [[#compile][Compile]]
  - [[#terminals--shells][Terminals & Shells:]]
    - [[#exec-path-from-shell][Exec-path-from-shell]]
  - [[#project-management][Project management:]]
    - [[#dired][Dired]]
  - [[#backup][Backup:]]
    - [[#version-control-system][Version control system]]
  - [[#programming-languages][Programming languages:]]
    - [[#org-mode][Org-mode]]

** Initialization:

*** Lexical binding

| source |  [[https://github.com/bbatsov/emacs-lisp-style-guide#source-code-layout--organization][emacs-lisp-style-guide]] |

#+begin_src emacs-lisp
;;; -*- lexical-binding: t; -*-
#+end_src

*** Garbage collection Optimization

| source | [[https://github.com/hlissner/doom-emacs/blob/develop/docs/faq.org#how-does-doom-start-up-so-quickly][How does Doom start up so quickly?]] |

**** Startup

Restore the values of =gc-cons-threshold= and =file-name-handler-alist= after init.

#+begin_src emacs-lisp
(add-hook 'emacs-startup-hook
          (lambda ()
            (setq gc-cons-threshold user/gc-cons-threshold)
            (setq gc-cons-percentage 0.1)
            (setq file-name-handler-alist user/file-name-handler-alist)))
#+end_src

**** Minibuffer

Raise =gc-cons-threshold= while the minibuffer is active, so the GC doesn’t slow
down expensive commands.

#+begin_src emacs-lisp
(defun user/minibuffer-defer-garbage-collection ()
  "Raise `gc-cons-threshold' while the minibuffer is active,
this would benefit expensive commands or completion frameworks."
  (setq gc-cons-threshold most-positive-fixnum))   ; 2^61 bytes

(defun user/minibuffer-restore-garbage-collection ()
  "Defer it so that commands launched immediately after will enjoy the benefits."
  (run-at-time 1 nil (lambda () (setq gc-cons-threshold user/gc-cons-threshold))))

(add-hook 'minibuffer-setup-hook #'user/minibuffer-defer-garbage-collection)
(add-hook 'minibuffer-exit-hook  #'user/minibuffer-restore-garbage-collection)
#+end_src

*** Directory management

#+begin_src emacs-lisp
(defconst user-data-dir
  (eval-when-compile (concat (file-truename user-emacs-directory) ".cache/"))
  "Provide a location where Emacs can store static and dynamic data.")

;; Automatically create missing directories.
(dolist (dir (list user-data-dir))
  (unless (file-directory-p dir)
    (make-directory dir 'parents)))  
#+end_src

*** Custom

Inhibit the =custom-set-variables= block in the init file.

#+begin_src emacs-lisp
(setq custom-file (concat user-data-dir "custom.el"))
(when (file-exists-p custom-file)
  (load custom-file t t))
#+end_src

*** Libraries

| source | [[https://github.com/dieggsy/dotfiles/blob/master/emacs/.emacs.d/init.org][dieggsy/dotfiles]] |

Built-in utilities libraries that add a wealth of common-lisp inspired
functions and macros. These libraries will be require by other packages.

#+begin_src emacs-lisp
(require 'cl-lib)
(require 'subr-x)
#+end_src

** Package management:

*** Package

Emacs's built-in package management.

#+begin_src emacs-lisp
(require 'package)

;; Loads whichever version of the file is newest.
(setq load-prefer-newer t)

;; Tell package.el where to store Emacs Lisp code.
(setq package-user-dir (concat user-data-dir "elpa/"))

;; Adding a list of repositories.
(setq package-archives '(("org"   . "http://orgmode.org/elpa/")
                         ("gnu"   . "http://elpa.gnu.org/packages/")
                         ("melpa" . "https://melpa.org/packages/")))
#+end_src

*** Use-package

| source | [[https://github.com/jwiegley/use-package][jwiegley/use-package]] |

#+begin_quote
A use-package declaration for simplifying your .emacs 
#+end_quote

#+begin_src emacs-lisp
(eval-when-compile
  ;; Ensure that `use-package' and dependencies are installed.
  (unless package--initialized (package-initialize))
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package)
    (package-install 'bind-key)
    (package-install 'diminish))
  (require 'use-package)
  (require 'bind-key)
  (require 'diminish))

;; Control how use-package.el handle packages.
(setq use-package-always-ensure t)
(setq use-package-always-defer t)
(setq use-package-always-demand nil)
(setq use-package-hook-name-suffix nil)
#+end_src

** UI Enhancements:

*** Frame and buffer

#+begin_src emacs-lisp
;; A simple frame title.
(setq frame-title-format '("Emacs"))

;; Update UI less frequently.
(setq idle-update-delay 1.0)
(setq jit-lock-defer-time 0)

;; Give each frame/window the same number of pixels.
(setq frame-resize-pixelwise t)
(setq window-resize-pixelwise t)

;; Minimal startup screen/message.
(setq inhibit-default-init t)
(setq inhibit-splash-screen t)
(setq inhibit-startup-screen t)
(setq inhibit-startup-message t)
(setq inhibit-startup-echo-area-message t)

;; Inhibit the "For information about GNU Emacs..." message at startup.
(advice-add #'display-startup-echo-area-message :override #'ignore)

;; Hiding Scrollbar, tool bar, and menu.
(tool-bar-mode   -1)
(scroll-bar-mode -1)
(menu-bar-mode   -1)

;; Disable UI dialog.
(setq use-dialog-box nil)
(setq show-help-function nil)

;; Disable bell (both visual and audible).
(setq ring-bell-function #'ignore)
(setq visible-bell nil)
#+end_src

*** Font

#+begin_src emacs-lisp
;; utf-8 coding system.
(when (fboundp 'set-charset-priority)
  (set-charset-priority 'unicode))
(prefer-coding-system 'utf-8)
(setq locale-coding-system 'utf-8)

;; main typeface
(set-face-attribute 'default nil
                    :family "fira code"
                    :height 120
                    :weight 'normal
                    :width  'normal)

;; proportionately spaced typeface
(set-face-attribute 'variable-pitch nil :family "fira code" :height 1.0)

;; monospaced typeface
(set-face-attribute 'fixed-pitch nil :family "fira code" :height 1.0)

;; don’t compact font caches during garbage collection.
(setq inhibit-compacting-font-caches t)
#+end_src

*** Minibuffer

#+begin_src emacs-lisp
;; show keystrokes in progress instantly.
(setq echo-keystrokes 0.02)

;; enable recursive minibuffers.
(setq enable-recursive-minibuffers t)

;; keep the cursor out of the minibuffer.
(setq minibuffer-prompt-properties '(read-only t intangible t cursor-intangible t face minibuffer-prompt))

;; expand the minibuffer to fit multi-line text displayed in the echo-area.
(setq max-mini-window-height 0.12)
(setq resize-mini-windows 'grow-only)

;; use y / n instead of yes / no.
(setq confirm-kill-emacs #'y-or-n-p)
(fset #'yes-or-no-p #'y-or-n-p)
#+end_src

*** Cursor

#+begin_src emacs-lisp
;; less distracting settings.
(blink-cursor-mode -1)
(setq blink-matching-paren nil)

;; display the current column number.
(setq column-number-mode t)

;; don't stretch the cursor to fit wide characters.
(setq x-stretch-cursor nil)

;; keep cursor at end of lines.
(setq-default track-eol t)
(setq-default line-move-visual nil)

;; inhibit rendering the cursor in non-focused windows.
(setq-default cursor-in-non-selected-windows nil)
(setq highlight-nonselected-windows nil)
#+end_src

*** Scrolling

| source | [[https://github.com/matthewzmd/.emacs.d#smooth-scrolling][matthewzmd/.emacs.d#smooth-scrolling]] |

#+begin_src emacs-lisp
;; vertical scroll
(setq scroll-step 1)
(setq scroll-margin 0)
(setq scroll-conservatively 101)
(setq scroll-up-aggressively 0.01)
(setq scroll-down-aggressively 0.01)
(setq auto-window-vscroll nil)
(setq fast-but-imprecise-scrolling nil)
(setq mouse-wheel-scroll-amount '(1 ((shift) . 1)))
(setq mouse-wheel-progressive-speed nil)

;; horizontal scroll
(setq hscroll-step 1)
(setq hscroll-margin 1)
#+end_src

*** Fringe

#+begin_src emacs-lisp
;; make the right fringe 8 pixels wide and the left disappear.
(fringe-mode '(8 . 8))

;; reserve the fringe for more useful information.
(setq indicate-empty-lines nil)
(setq indicate-buffer-boundaries nil)
#+end_src

*** General coding style

#+begin_src emacs-lisp
;; use spaces for indentation. no hard tabs.
(setq-default indent-tabs-mode nil)
(setq-default tab-width 4)

;; indent empty string and enable tab completion.
(setq-default tab-always-indent 'complete)

;; inhibit wrapping words/lines by default.
(setq-default truncate-lines t)
(setq-default truncate-partial-width-windows nil)

;; assume that sentences end with one space rather than two.
(setq sentence-end-double-space nil)

;; best practice following the posix standard.
;; <https://stackoverflow.com/questions/729692/>
(setq require-final-newline t)

;; <https://www.gnu.org/software/emacs/manual/html_node/emacs/apropos.html>
;; Make apropos more useful.
(setq apropos-do-all t)
#+end_src

** Colorscheme

*** Modus-themes

| source | [[https://protesilaos.com/modus-themes/][protesilaos/modus-themes]] |

#+begin_quote
Accessible themes for GNU Emacs, conforming with the highest
accessibility standard for colour contrast between background
and foreground values (WCAG AAA).
#+end_quote

#+begin_src emacs-lisp
(use-package modus-themes
  :demand t
  :init (setq modus-themes-org-blocks 'grayscale)
  :config (load-theme 'modus-operandi t))
#+end_src

** Keybindings:

*** Global settings

Better super/meta keys position on apple.

| source | [[https://emacs.stackexchange.com/questions/26616][questions/26616]] |

#+begin_src emacs-lisp
(when (eq system-type 'darwin)
  (setq mac-control-modifier nil)
  (setq mac-option-modifier  'meta)
  (setq mac-command-modifier 'control))
#+end_src

Make =esc= quit prompts.

#+begin_src emacs-lisp
(global-set-key (kbd "<escape>") 'keyboard-escape-quit)  
#+end_src

**** General

| source | [[https://github.com/noctuid/general.el][noctuid/general.el]] |

#+begin_quote
More convenient key definitions in emacs
#+end_quote

#+begin_src emacs-lisp
(use-package general
  :demand t
  :config
  (general-evil-setup t)
  (general-override-mode)

  ;; The leader prefix key for Evil users.
  (general-create-definer user/leader-key
    :states '(normal visual)
    :keymaps 'override
    :prefix "SPC")

  ;; An alternative leader prefix key used for Insert and Emacs states.
  (general-create-definer user/leader-alt-key
    :states '(insert emacs)
    :keymaps 'override
    :non-normal-prefix "M-SPC"))
#+end_src

*** Modal editing

**** Evil-mode

| source | [[https://github.com/emacs-evil/evil][emacs-evil/evil]] |

#+begin_quote
The extensible vi layer for Emacs.
#+end_quote

#+begin_src emacs-lisp
(use-package evil
  :demand t
  ;; :general
  ;; TODO:
  ;; (nmap
  ;;   "j" 'evil-next-visual-line
  ;;   "k" 'evil-previous-visual-line)
  :init
  ;; Needed by evil-collection package.
  (setq evil-want-integration t)
  (setq evil-want-keybinding nil)
  ;; Keybindings and other behaviour.
  (setq evil-want-C-i-jump t)
  (setq evil-want-C-u-scroll t)
  (setq evil-want-C-d-scroll t)
  (setq evil-want-Y-yank-to-eol t)
  ;; Miscellaneous
  (setq evil-esc-delay 0)
  (setq evil-echo-state t)
  ;; Cursor movement
  (setq evil-track-eol t)
  (setq evil-move-cursor-back nil)
  ;; Indentation
  (setq evil-auto-indent t)
  (setq evil-shift-width 4)
  (setq evil-shift-round t)
  (setq evil-indent-convert-tabs t)
  ;; Window management
  (setq evil-split-window-below t)
  (setq evil-vsplit-window-right t)
  ;; Search
  (setq evil-symbol-word-search t)
  (setq evil-ex-visual-char-range t)
  (setq evil-ex-search-vim-style-regexp t)
  (setq evil-ex-interactive-search-highlight 'selected-window)
  :config
  ;; The initial state of certain mode.
  (evil-set-initial-state 'help-mode  'motion)
  (evil-set-initial-state 'nrepl-mode 'insert)
  (evil-set-initial-state 'dired-mode 'emacs)
  ;; (evil-set-initial-state 'org-mode 'emacs)

  ;; Allows you to click buttons without initiating a selection.
  (define-key evil-motion-state-map [down-mouse-1] nil)
  (evil-mode 1))
#+end_src

**** Evil-collection

| source | [[https://github.com/emacs-evil/evil-collection][emacs-evil/evil-collection]] |

#+begin_quote
A set of keybindings for evil-mode.
#+end_quote

#+begin_src emacs-lisp
(use-package evil-collection
  :demand t
  :after (evil)
  :config
  (evil-collection-init))
#+end_src


**** Evil-matchit

| source | [[https://github.com/redguardtoo/evil-matchit][redguardtoo/evil-matchit]] |

#+begin_quote
Vim matchit ported into Emacs
#+end_quote

#+begin_src emacs-lisp
(use-package evil-matchit
  :demand t
  :after (evil)
  :config
  (global-evil-matchit-mode 1))
#+end_src

*** Mnemonic interfaces

**** Which-key 

| source | [[https://github.com/justbur/emacs-which-key][justbur/emacs-which-key]] |

#+begin_quote
emacs package that displays available keybindings in popup.
#+end_quote

#+begin_src emacs-lisp
(use-package which-key
  :defer 1
  :diminish
  :config
  (setq which-key-separator " → ")
  (setq which-key-min-display-lines 6)
  (setq which-key-add-column-padding 1)
  (setq which-key-sort-uppercase-first nil)
  (setq which-key-sort-order #'which-key-prefix-then-key-order)
  (set-face-attribute 'which-key-local-map-description-face nil :weight 'bold)
  (which-key-setup-side-window-bottom)
  (which-key-mode 1))
#+end_src

**** Hydra

| source | [[https://github.com/abo-abo/hydra][abo-abo/hydra]] | 

#+begin_quote
Make Emacs bindings that stick around.
#+end_quote

#+begin_src emacs-lisp
(use-package hydra
  :defer 1
  :diminish)
#+end_src

** Built-in packages:

*** Saveplace

#+begin_quote
Automatically save place in files, so that visiting them later
(even during a different Emacs session) automatically moves point
to the saved position, when the file is first found.
#+end_quote

#+begin_src emacs-lisp
(use-package saveplace
  :ensure nil
  :demand t
  :config
  (setq save-place-file (concat user-data-dir "saveplace"))
  (setq save-place-limit 100)
  (save-place-mode 1))
#+end_src

*** Paren-mode

#+begin_quote
It will display highlighting on whatever paren matches the one before or 
after point.
#+end_quote

#+begin_src emacs-lisp
(use-package paren
  :ensure nil
  :defer 1
  :hook (prog-mode-hook . show-paren-mode)
  :config
  (setq show-paren-delay 0.1)
  (setq show-paren-highlight-openparen t)
  (setq show-paren-when-point-inside-paren t)
  (setq show-paren-when-point-in-periphery t))
#+end_src

*** recentf

#+begin_quote
This package maintains a menu for visiting files that were operated
on recently.
#+end_quote

#+begin_src emacs-lisp
(use-package recentf
  :ensure nil
  :defer 1
  :config
  (setq recentf-save-file (concat user-data-dir "recentf"))
  (setq recentf-auto-cleanup 'never)
  (setq recentf-max-saved-items 300)
  (setq recentf-max-menu-items 0)
  (recentf-mode 1))
#+end_src

*** Autorevert

#+begin_quote
... Automatically revert buffers whenever the corresponding files have been 
changed on disk and the buffer contains no unsaved changes.
#+end_quote

#+begin_src emacs-lisp
(use-package autorevert
  :ensure nil
  :defer 1
  :diminish
  :config
  (setq auto-revert-verbose t)
  (setq auto-revert-use-notify nil)
  (setq auto-revert-check-vc-info t)
  (setq revert-without-query (list "."))
  (global-auto-revert-mode 1))
#+end_src

*** savehist

#+begin_quote
Many editors (e.g. Vim) have the feature of saving minibuffer
history to an external file after exit. This package provides the
same feature in Emacs.
#+end_quote

#+begin_src emacs-lisp
(use-package savehist
  :ensure nil
  :defer 3
  :config
  (setq savehist-file (concat user-data-dir "savehist"))
  (setq savehist-save-minibuffer-history t)
  (setq savehist-autosave-interval 60)
  (setq savehist-additional-variables
        '(kill-ring                   ; persist clipboard
          search-ring                 ; persist searches
          regexp-search-ring))
  (savehist-mode 1))
#+end_src

*** Eldoc

#+begin_quote
This program was inspired by the behavior of the "mouse documentation window" 
on many Lisp Machine systems, as you type a function's symbol name
as part of a sexp, it will print the argument list for that function.
#+end_quote

#+begin_src emacs-lisp
(use-package eldoc
  :ensure nil
  :defer 5
  :diminish
  :hook (prog-mode-hook . eldoc-mode)
  :config
  (setq eldoc-idle-delay 0.2)
  (setq eldoc-echo-area-use-multiline-p nil)
  (global-eldoc-mode 1))
#+end_src

*** Compile

#+begin_quote
This package provides the compile facilities documented in the Emacs user's manual.
#+end_quote

#+begin_src emacs-lisp
(use-package compile
  :ensure nil
  :defer 5
  :config
  (setq compilation-always-kill t)
  (setq compilation-ask-about-save nil)
  (setq compilation-scroll-output 'first-error))
#+end_src

** Terminals & Shells:

*** Exec-path-from-shell

| source | [[https://github.com/purcell/exec-path-from-shell][purcell/exec-path-from-shell]] |

#+begin_quote
Make Emacs use the $PATH set up by the user's shell.
#+end_quote

#+begin_src emacs-lisp
(when (eq system-type 'darwin)
  (use-package exec-path-from-shell
    :demand t
    :config
    (setq exec-path-from-shell-variables '("PATH"))
    (exec-path-from-shell-initialize)))
#+end_src

** Project management:

*** Dired

#+begin_quote
This is a major mode for directory browsing and editing.
#+end_quote

#+begin_src emacs-lisp
(use-package dired
  :ensure nil
  :hook ((dired-mode-hook . auto-revert-mode)
         (dired-mode-hook . dired-hide-details-mode))
  :init
  (setq dired-dwim-target t)
  (setq dired-use-ls-dired nil)
  (setq dired-auto-revert-buffer t)
  (setq dired-hide-details-hide-symlink-targets nil)
  (setq dired-listing-switches "-alh --group-directories-first")
  ;; Always copy/delete recursively
  (setq dired-recursive-copies  'always)
  (setq dired-recursive-deletes 'top)
  :config

  (defhydra hydra-dired (:hint nil :color pink)
    "
_+_ mkdir          _v_iew           _m_ark             _(_ details        _i_nsert-subdir    wdired
_C_opy             _O_ view other   _U_nmark all       _)_ omit-mode      _$_ hide-subdir    C-x C-q : edit
_D_elete           _o_pen other     _u_nmark           _l_ redisplay      _w_ kill-subdir    C-c C-c : commit
_R_ename           _M_ chmod        _t_oggle           _g_ revert buf     _e_ ediff          C-c ESC : abort
_Y_ rel symlink    _G_ chgrp        _E_xtension mark   _s_ort             _=_ pdiff
_S_ymlink          ^ ^              _F_ind marked      _._ toggle hydra   \\ flyspell
_r_sync            ^ ^              ^ ^                ^ ^                _?_ summary
_z_ compress-file  _A_ find regexp
_Z_ compress       _Q_ repl regexp
T - tag prefix
"
    ("\\" dired-do-ispell)
    ("(" dired-hide-details-mode)
    (")" dired-omit-mode)
    ("+" dired-create-directory)
    ("=" diredp-ediff)         ;; smart diff
    ("?" dired-summary)
    ("$" diredp-hide-subdir-nomove)
    ("A" dired-do-find-regexp)
    ("C" dired-do-copy)        ;; Copy all marked files
    ("D" dired-do-delete)
    ("E" dired-mark-extension)
    ("e" dired-ediff-files)
    ("F" dired-do-find-marked-files)
    ("G" dired-do-chgrp)
    ("g" revert-buffer)        ;; read all directories again (refresh)
    ("i" dired-maybe-insert-subdir)
    ("l" dired-do-redisplay)   ;; relist the marked or singel directory
    ("M" dired-do-chmod)
    ("m" dired-mark)
    ("O" dired-display-file)
    ("o" dired-find-file-other-window)
    ("Q" dired-do-find-regexp-and-replace)
    ("R" dired-do-rename)
    ("r" dired-do-rsynch)
    ("S" dired-do-symlink)
    ("s" dired-sort-toggle-or-edit)
    ("t" dired-toggle-marks)
    ("U" dired-unmark-all-marks)
    ("u" dired-unmark)
    ("v" dired-view-file)      ;; q to exit, s to search, = gets line #
    ("w" dired-kill-subdir)
    ("Y" dired-do-relsymlink)
    ("z" diredp-compress-this-file)
    ("Z" dired-do-compress)
    ("q" nil)
    ("." nil :color blue))

  (define-key dired-mode-map "?" 'hydra-dired/body))
#+end_src

** Backup:

Don't save anything or create lock/history/backup files.

| source | [[https://www.emacswiki.org/emacs/BackupDirectory][Emacs Wiki]] |

#+begin_src emacs-lisp
(setq create-lockfiles nil)
(setq make-backup-files nil)
(setq auto-save-default nil)
(setq auto-save-list-file-prefix nil)
#+end_src

*** Version control system

Preference VCS over Emacs built-in backups tools.

#+begin_src emacs-lisp
(setq version-control t)
(setq vc-follow-symlinks t)
(setq delete-old-versions t)
#+end_src

Convenient UI to browse through the differences between files or buffers.

#+begin_src emacs-lisp
(setq ediff-diff-options "-w")  ; turn off whitespace checking.
(setq ediff-split-window-function #'split-window-horizontally)
(setq ediff-window-setup-function #'ediff-setup-windows-plain)
#+end_src

**** Magit

| source | [[https://magit.vc/][magit/magit]] |

#+begin_quote
It's Magit! A Git porcelain inside Emacs.
#+end_quote

#+begin_src emacs-lisp
(use-package magit
  :defer 1
  :init
  ;; Must be set early to prevent ~/.emacs.d/transient from being created.
  (setq transient-levels-file  (concat user-data-dir "transient/levels"))
  (setq transient-values-file  (concat user-data-dir "transient/values"))
  (setq transient-history-file (concat user-data-dir "transient/history")))
#+end_src

** Programming languages:

*** Org-mode

| source | [[https://orgmode.org/index.html][bzg/org-mode]] |

#+begin_quote
Org mode is for keeping notes, maintaining TODO lists, planning projects, 
and authoring documents with a fast and effective plain-text system.
#+end_quote

#+begin_src emacs-lisp
(defun user/org-mode-setup ()
"Create a minimalistic user experience by disabling certain minor settings."
(org-indent-mode)
(setq require-final-newline nil))
#+end_src

#+begin_src emacs-lisp
(use-package org
  :hook (org-mode-hook . user/org-mode-setup)
  :config
  ;; Insead of "..." show "…" when there's hidden folded content
  ;; Some characters to choose from: …, ⤵, ▼, ↴, ⬎, ⤷, and ⋱
  (setq org-ellipsis " ⤵")

  ;; Blocks
  (setq org-hide-block-startup t)
  ;; Markers
  (setq org-hide-emphasis-markers t)
  (setq org-catch-invisible-edits 'show)
  ;; List
  (setq org-list-allow-alphabetical t)
  ;; Leading stars
  (setq org-hide-leading-stars t)
  (setq org-hide-leading-stars-before-indent-mode t)
  ;; Fontify
  (setq org-return-follows-link t)
  (setq org-fontify-done-headline t)
  (setq org-fontify-quote-and-verse-blocks t)
  ;; Source code blocks
  (setq org-src-fontify-natively t)
  (setq org-src-tab-acts-natively t)
  (setq org-src-preserve-indentation t)
  (setq org-edit-src-content-indentation 0)
  ;; Checkbox behavior
  (setq org-enforce-todo-dependencies t)
  (setq org-enforce-todo-checkbox-dependencies t))

(org-babel-do-load-languages
 'org-babel-load-languages
 '((emacs-lisp . t)))

(setq org-confirm-babel-evaluate nil)
#+end_src

#+begin_src emacs-lisp
(require 'org-tempo)

(add-to-list 'org-structure-template-alist '("org" . "src org-mode"))
(add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
#+end_src

**** Toc-org

| source | [[https://github.com/snosov1/toc-org][snosov1/toc-org]] |

#+begin_quote
toc-org is an Emacs utility to have an up-to-date table of contents in the org
files without exporting (useful primarily for readme files on GitHub).
#+end_quote

#+begin_src emacs-lisp
(use-package toc-org
  :hook (org-mode-hook . toc-org-mode))
#+end_src

**** Auto tangle configuration file

#+begin_src emacs-lisp
(defun auto-org-babel-tangle-config ()
  "Automatically tangle our .org config file when we save it."
  (when (string-equal (buffer-file-name)
                      (expand-file-name "~/.emacs.d/literate_configuration/configuration.org"))
    ;;Dynamic scoping to the rescue.
    (let ((org-confirm-babel-evaluate nil))
      (org-babel-tangle))))

(add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'auto-org-babel-tangle-config)))
#+end_src